group = 'com.github.bangroot.vertx'

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1"
  }
}

// Apply the groovy plugin to add support for Groovy
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: "jacoco"
apply plugin: "maven"
apply plugin: "maven-publish"
apply plugin: "eclipse"
apply from: 'gradle/wrapper.gradle'

// In this section you declare where to find the dependencies of your project
repositories {
  // Use 'maven central' for resolving your dependencies.
  // You can declare any Maven/Ivy/file repository here.
  jcenter()
  mavenCentral()
}

configurations {
  published
  provided
  compile.extendsFrom provided
}

conf2ScopeMappings.addMapping(0, configurations.provided, Conf2ScopeMappingContainer.PROVIDED)

// In this section you declare the dependencies for your production and test code
dependencies {
  // We use the latest groovy 2.x version for building this library
  provided 'org.codehaus.groovy:groovy-all:2.1.5'
  provided "io.vertx:vertx-core:3.0.0"
  provided "io.vertx:vertx-lang-groovy:3.0.0"
  // We use the awesome Spock testing and specification framework
  testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
  testCompile 'cglib:cglib-nodep:3.1'
  testCompile 'org.objenesis:objenesis:2.1'
  testCompile "io.vertx:vertx-unit:3.0.0"
}

sourceSets {
  test {
    resources {
      srcDir 'src/main/jar'
    }
  }
}

jar {
  metaInf {
    from("src/main/jar/META-INF") {
      include "**/*.*"
    }
  }
}

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

test {
}

publishing {
  publications {
    published(MavenPublication) {
      from components.java
      artifact sourceJar
      artifact javadocJar
    }
  }
}
// Add the sourceJars to non-extractor modules
artifacts {
  published sourceJar
  published javadocJar
}

apply from: 'gradle/bintray.gradle'
apply from: 'gradle/artifactory.gradle'
